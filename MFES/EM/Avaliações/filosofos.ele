// Modelo do jantar dos filósofos

// As "coisas" à volta da mesa
abstract sig Coisa {
	prox : one Coisa
}

sig Filosofo extends Coisa {
	// Garfos que cada filósofo tem na mão
	var garfos : set Garfo
}

sig Garfo extends Coisa {}

fact Mesa {
	// A mesa é redonda, ou seja, as coisas formam um anel
	all c : Coisa | Coisa in c.^prox
	// Os garfos e os filósofos estão intercalados
	all c : Coisa | c in Filosofo iff c.prox in Garfo
}

// Especifique os seguintes eventos

// Um filosofo pode comer se já tiver os dois garfos junto a si
// e pousa os garfos depois
pred come [f : Filosofo] {
	some disj g1,g2 : f.garfos {
		garfos' = garfos - (f->g1) - (f->g2)
	}
}

// Um filósofo pode pegar num dos garfos que estejam
// pousados junto a si
pred pega [f : Filosofo] {
	some g : Garfo {
		g not in Filosofo.garfos
		g in f.prox + prox.f
		garfos' = garfos + (f->g)
	}
}

// Para além de comer ou pegar em garfos os filósofos podem pensar
pred pensa [f : Filosofo] {
	garfos' = garfos
}

fact Comportamento {
	// No inicio os garfos estão todos pousados
	no garfos
	// Depois os filósfos só podem comer, pegar ou pensar
	always (some f : Filosofo | come[f] or pega[f] or pensa[f])
}

// Especifique as seguintes propriedades

assert GarfosNaMao {
	// O mesmo garfo nunca pode estar na mão de dois filósofos
	always(garfos.~garfos in iden)
}
check GarfosNaMao for 6

assert SempreQuePegaCome {
	// Qualquer filósofo que pega num garfo vai conseguir comer
	always(all f : Filosofo | pega[f] implies eventually come[f])
}
check SempreQuePegaCome for 6

pred comeEnabled[f: Filosofo] {
	some disj g1, g2: f.garfos | some g1 and some g2
}

pred pegaEnabled[f: Filosofo] {
	some g: Garfo | g not in Filosofo.garfos and g in f.prox + f.~prox
}

assert SemBloqueio {
	// O sistema não pode bloquear numa situação em só é possível pensar! POSSIBILIDADE
	always(some f : Filosofo | pegaEnabled[f] or comeEnabled[f])

}
check SemBloqueio for 6

// Especifique um cenário com 4 filósofos onde todos conseguem comer
run Exemplo {
	all f: Filosofo | eventually come[f]
} for exactly 4 Filosofo, exactly 8 Coisa, exactly 4 Garfo, exactly 12 steps
